plugins {
    id "com.android.library"
    id "maven-publish"
}

android {
    compileSdkVersion 31

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 31
    }
    lintOptions {
        abortOnError false
    }
}

repositories {
    if (project == rootProject) {
        maven { url "https://a8c-libs.s3.amazonaws.com/android/react-native-mirror" }
    } else {
        // When building as a dep, the RN's maven repo is locally in the node_modules folder
        def nodeModulesPath = "${project.buildDir}/../../node_modules/"
        maven { url "${nodeModulesPath}/react-native/android" }
    }

    google()
    jcenter()
    mavenCentral()
}

dependencies {
    if (project == rootProject) {
        // If this is the root project (e.g. Jitpack), specify a version
        implementation 'com.facebook.react:react-native:0.66.2'
    } else {
        //noinspection GradleDynamicVersion
        api "com.facebook.react:react-native:+"
    }
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = 'com.github.wordpress-mobile'
                artifactId = 'react-native-linear-gradient'
            }
        }
    }
}
